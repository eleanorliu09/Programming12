import fisica.*;
FWorld world;

// KEYBOARD
boolean wkey, akey, skey, dkey, upkey, downkey, rightkey, leftkey;
boolean lr; // true = left, false = right

// COLORS
color skyblue = #d9f4ff;
color yellowgreen = #aff24b;
color orange = #ffc061;
color white = #FFFFFF;
color green = #00d458;

// COUNTERS
int rightScore, leftScore;
int counter;

// FBodies
FBox leftPlayer, rightPlayer, leftGround, rightGround, net;
FCircle ball;

void setup() {
  size(800, 600);
  createWorld();
  createBodies();
  rectMode(CENTER);
  leftScore = 0;
  rightScore = 0;
  lr = true;
  counter = 180;
}

void draw() {
  background(skyblue);
  handlePlayerInput();
  showScores();
  runWorld();
  countdown();
}

void countdown() {
  counter--;
  if (counter > 0) {
    fill(white, 128);
    noStroke();
    rect(0.5*width, 0.5*height, width, height);
    if (counter > 120) {
      textSize(90);
      fill(green);
      text("3", 0.5*width, 0.5*height);
    } else if (counter <= 120 && counter > 60) {
      textSize(90);
      fill(green);
      text("2", 0.5*width, 0.5*height);
    } else if (counter <= 60 && counter > 0) {
      textSize(90);
      fill(green);
      text("1", 0.5*width, 0.5*height);
    }
  }
}

void createWorld() {
  Fisica.init(this);
  world = new FWorld();
  world.setGravity(0, 980);
  world.setEdges(skyblue);
  world.setEdgesRestitution(0);
}

void runWorld() {
  world.step();  //get box2D to calculate all the forces and new positions

  if (counter < 0) {
    world.draw();  //ask box2D to convert this world to processing screen coordinates and draw
  }
}

void reset() {
  println("r");
  leftPlayer.setPosition(0.25*width, 500);
  leftPlayer.setVelocity(0, 0);
  rightPlayer.setPosition(0.75*width, 500);
  rightPlayer.setVelocity(0, 0);
  ball.setVelocity(0, 0);
  if (lr) ball.setPosition(0.25*width, 220);
  else ball.setPosition(0.75*width, 220);
}

void showScores() {
  if (hitGround(leftGround)) {
    rightScore++;
    reset();
    lr = true;
  }
  if (hitGround(rightGround)) {
    leftScore++;
    reset();
    lr = false;
  }
  textSize(40);
  fill(#000000);
  text(rightScore, 0.75*width, 80);
  text(leftScore, 0.25*width, 80);
}
